/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply from: "$rootDir/config/gradle/js/build-tasks.gradle"
apply plugin: 'com.google.protobuf'

ext {
    testSrcDir = "$projectDir/test"
    genProtoBaseDir = projectDir
    genProtoSubDir = "proto"
    genProtoTestDir = "$testSrcDir/$genProtoSubDir"
    nycOutputDir = "$projectDir/.nyc_output"
    packageLockFile = "$projectDir/package-lock.json"
}

dependencies {
    testProtobuf project(":test-app")
}

/**
 *
 */
task deleteCompiled {
    description = 'Cleans old module dependencies and build outputs.'

    clean.dependsOn deleteCompiled

    doLast {
        delete genProtoTestDir
        delete packageLockFile
        delete nycOutputDir
    }
}

/**
 *
 */
task installUnpublishedLib {
    description = 'Install unpublished artifact of `spine-web` library as a module dependency'

    dependsOn ':client-js:link'

    doLast {
        npm 'run', 'installUnpublishedLib'
    }
}

/**
 *
 */
task integrationTest {
    description = 'Runs integration tests of the `spine-web` library agains the test application.'

    dependsOn build, installUnpublishedLib, ':test-app:appBeforeIntegrationTest'
    finalizedBy ':test-app:appAfterIntegrationTest'

    doLast {
        npm 'run', 'test'
    }
}

/**
 *
 */
task integrationTestCoverage {
    description = 'Runs tests of the `spine-web` library with coverage.'

    dependsOn build, installUnpublishedLib, ':test-app:appBeforeIntegrationTest'
    finalizedBy ':test-app:appAfterIntegrationTest'

    final def runsOnWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    final def coverageScript = runsOnWindows ? 'coverage:win' : 'coverage:unix'

    doLast {
        npm 'run', coverageScript
    }
}

protobuf {
    generatedFilesBaseDir = genProtoBaseDir
    protoc {
        artifact = deps.build.protoc
    }
    generateProtoTasks {
        all().each { final task ->
            task.builtins {
                // Do not use java builtin output in this project.
                remove java

                // For information on JavaScript code generation please see
                // https://github.com/google/protobuf/blob/master/js/README.md
                js {
                    option "import_style=commonjs"
                    outputSubDir = genProtoSubDir
                }

                task.generateDescriptorSet = true
                final def testClassifier = task.sourceSet.name == "test" ? "_test" : ""
                final def descriptorName = "${project.group}_${project.name}_${project.version}${testClassifier}.desc"
                task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${task.sourceSet.name}/${descriptorName}"
            }
            compileProtoToJs.dependsOn task
        }
    }
}

apply plugin: 'io.spine.tools.proto-js-plugin'

protoJs {
    testGenProtoDir = genProtoTestDir

    generateParsersTask().dependsOn compileProtoToJs
    buildJs.dependsOn generateParsersTask()
}

idea.module {
    testSourceDirs += file(testSrcDir)

    excludeDirs += file(genProtoTestDir)

    iml {
        beforeMerged { module ->
            module.dependencies.clear()
        }
        whenMerged { module ->
            module.dependencies*.exported = true
        }
    }
}

// Suppress building the JS project as a Java module.
project.compileJava.enabled = false
project.compileTestJava.enabled = false
