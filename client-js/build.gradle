/*
 * Copyright 2020, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

import com.google.common.io.Files

apply from: "$rootDir/scripts/js.gradle"

apply plugin: 'com.google.protobuf'

dependencies {
    protobuf project(':web')
    protobuf group: 'io.spine', name: 'spine-client', version: spineCoreVersion, classifier: 'proto'
    testProtobuf group: 'io.spine', name: 'spine-client', version: spineCoreVersion, classifier: 'proto'
}

idea.module {
    sourceDirs += file(srcDir)
    testSourceDirs += file(testSrcDir)

    excludeDirs += file(nycOutputDir)
    excludeDirs += file(genProtoMain)
    excludeDirs += file(genProtoTest)

    iml {
        beforeMerged { module ->
            module.dependencies.clear()
        }
        whenMerged { module ->
            module.dependencies*.exported = true
        }
    }
}

sourceSets {
    main {
        java.srcDirs = []
        resources.srcDirs = []
    }
    test {
        java.srcDirs = []
        resources.srcDirs = []
    }
}

// Suppress building the JS project as a Java module.
project.compileJava.enabled = false
project.compileTestJava.enabled = false

final File jsDocDir = Files.createTempDir()

afterEvaluate {
    generatedDocs += files(jsDocDir)
}

task jsDoc(type: Exec) {
    commandLine 'jsdoc', '-d', jsDocDir.path, "$projectDir/main/*"
}
